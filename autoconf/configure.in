AC_INIT()

is_abs=`echo ${top_srcdir-$srcdir} | sed -n 's/^\/.*/yes/p'`
if test "$is_abs" = "" ; then
	SRC_TOPDIR='${BUILD_TOPDIR}'/autoconf/${top_srcdir-$srcdir}/..
else
	SRC_TOPDIR=${top_srcdir-$srcdir}/..
fi
AC_SUBST(SRC_TOPDIR)

AC_CONFIG_AUX_DIR(${top_srcdir-$srcdir})

if test -d ../mkf ; then
	MKF_LIBS='${BUILD_TOPDIR}/mkf/lib/libmkf.la'
	MKF_CFLAGS='-I${BUILD_TOPDIR}/mkf/include'
else
	MKF_LIBS="-lmkf"
fi
AC_SUBST(MKF_LIBS)
AC_SUBST(MKF_CFLAGS)

if test -d ../kiklib ; then
	KIK_LIBS='${BUILD_TOPDIR}/kiklib/src/libkik.la'
	KIK_CFLAGS='-I${BUILD_TOPDIR}/kiklib/include'
else
	KIK_LIBS="-lkik"
fi
AC_SUBST(KIK_LIBS)
AC_SUBST(KIK_CFLAGS)

AC_PROG_CC
AC_PROG_INSTALL

AC_ARG_WITH(libtool,
	[  --with-libtool          libtool path[without]],
	libtool=$with_libtool)
if test "${libtool}" != "" ; then
	LIBTOOL=${libtool}
else
	AM_PROG_LIBTOOL
	LIBTOOL='${BUILD_TOPDIR}/autoconf/libtool'
fi
AC_SUBST(LIBTOOL)

AC_CHECK_LIB(socket,connect,SOCK_LIBS=-lsocket)
AC_SUBST(SOCK_LIBS)

AC_PATH_XTRA

# Li18nux Xlib-I18N defines X_HAVE_UTF8_STRING but has no Xutf8LookupString.
ldflags="$LDFLAGS" cflags="$CFLAGS"
LDFLAGS="$LDFLAGS $X_PRE_LIBS $X_LIBS $X_EXTRA_LIBS" CFLAGS="$X_CFLAGS $CFLAGS"
AC_CHECK_LIB(X11,Xutf8LookupString,XUTF8_CFLAGS="-DHAVE_XUTF8_LOOKUP_STRING")
LDFLAGS="$ldflags" CFLAGS="$cflags"
AC_SUBST(XUTF8_CFLAGS)

AC_ARG_ENABLE(debug,
	[  --enable-debug          debug[disabled]],
	debug=$enable_debug)
if test "$debug" = "yes" ; then
	DEB_CFLAGS="-DDEBUG -DKIK_DEBUG"
fi
AC_SUBST(DEB_CFLAGS)
AC_SUBST(OPT_CFLAGS)

AC_ARG_ENABLE(fribidi,
	[  --enable-fribidi        bidi[disabled]],
	fribidi=$enable_fribidi)
if test "$fribidi" = "yes" ; then
	AC_CHECK_PROG(fribidi_config,fribidi-config,yes)
	if test "$fribidi_config" = "yes" ; then
		FRIBIDI_CFLAGS="`fribidi-config --cflags` -DUSE_FRIBIDI"
		FRIBIDI_LIBS="`fribidi-config --libs`"
	fi
fi
AC_SUBST(FRIBIDI_CFLAGS)
AC_SUBST(FRIBIDI_LIBS)

# default value
IMAGELIB_NAME=none
AC_ARG_WITH(imagelib,
	[  --with-imagelib[=ARG]   image library [imlib, gdk-pixbuf2, gdk-pixbuf1]],
	imagelib=$with_imagelib)
case ${imagelib} in
imlib)
	AC_CHECK_PROG(imlib_config,imlib-config,yes)
	if test "$imlib_config" = "yes" ; then
		IMAGELIB_CFLAGS="`imlib-config --cflags` -DUSE_IMLIB"
		IMAGELIB_LIBS="`imlib-config --libs`"
		IMAGELIB_NAME=imlib
	fi
	;;
gdk-pixbuf2)
	AC_CHECK_PROG(pkg_config,pkg-config,yes)
	if test "$pkg_config" = "yes" ; then
		IMAGELIB_CFLAGS="`pkg-config --cflags gdk-pixbuf-2.0` -DUSE_GDK_PIXBUF"
		IMAGELIB_LIBS="`pkg-config --libs gdk-pixbuf-2.0`"
		IMAGELIB_NAME=gdk
	fi
	;;
gdk-pixbuf1)
	AC_CHECK_PROG(pkg_config,gdk-pixbuf-config,yes)
	if test "$pkg_config" = "yes" ; then
		IMAGELIB_CFLAGS="`gdk-pixbuf-config --cflags` -DUSE_GDK_PIXBUF -DOLD_GDK_PIXBUF"
		IMAGELIB_LIBS="`gdk-pixbuf-config --libs`"
		IMAGELIB_NAME=gdk
	fi
	;;
*)
	if test "${imagelib}" != "" ; then
		echo ""
		echo "${imagelib} library is NOT supported."
		echo ""
		exit 1
	fi
	;;
esac
AC_SUBST(IMAGELIB_CFLAGS)
AC_SUBST(IMAGELIB_LIBS)
AC_SUBST(IMAGELIB_NAME)

AC_ARG_ENABLE(anti_alias,
	[  --enable-anti-alias     anti alias[disabled]],
	anti_alias=$enable_anti_alias)
if test "$anti_alias" = "yes" ; then
	AC_CHECK_PROG(pkg_config,pkg-config,yes)
	if test "$pkg_config" = "yes" ; then
		if `pkg-config --exists xft` ; then
			AA_CFLAGS="`pkg-config --cflags xft` -DANTI_ALIAS"
			AA_LIBS="`pkg-config --libs xft`"
		else
			AA_CFLAGS="-DANTI_ALIAS"
			AA_LIBS="-lXft"
		fi
	else
		AA_CFLAGS="-DANTI_ALIAS"
		AA_LIBS="-lXft"
	fi
fi
AC_SUBST(AA_CFLAGS)
AC_SUBST(AA_LIBS)
AC_SUBST(PROG)

AC_ARG_ENABLE(ind,
	[  --enable-ind            libind[disabled]],
	iscii=$enable_ind)
if test "$enable_ind" = "yes" ; then
	AC_CHECK_LIB(ind,indian_init,
		[
		IND_CFLAGS="-DUSE_IND"
		IND_LIBS="-lind"
		])
fi
AC_SUBST(IND_LIBS)
AC_SUBST(IND_CFLAGS)

AC_ARG_ENABLE(utmp,
	[  --enable-utmp           utmp[disabled]],
	utmp=$enable_utmp)
if test "$utmp" = "yes" ; then
	UTMP_CFLAGS="-DUSE_UTMP"
	has_utmp=`grep utmp /etc/group 2>/dev/null`
	if test "$has_utmp" ; then
		INSTALL_OPT="-m 2755 -g utmp"
	else
		INSTALL_OPT="-m 4755 -o root"
	fi
else
	INSTALL_OPT="-m 755"
fi
AC_SUBST(UTMP_CFLAGS)
AC_SUBST(INSTALL_OPT)

AC_CHECK_PROG(gtk_config,gtk-config,yes)
if test "$gtk_config" = "yes" ; then
	AC_PATH_PROG(GTK_CONFIG,gtk-config,/usr/local/bin/gtk-config,$PATH)
	MAKE_DIRS="tool/mlconfig ${MAKE_DIRS}"
else
	# for Daichi Goto-san:)
	AC_CHECK_PROG(gtk_config,gtk12-config,yes)
	if test "$gtk_config" = "yes" ; then
		AC_PATH_PROG(GTK_CONFIG,gtk12-config,/usr/X11R6/bin/gtk12-config,$PATH)
		MAKE_DIRS="tool/mlconfig ${MAKE_DIRS}"
	fi
fi
AC_SUBST(GTK_CONFIG)

MAKE_DIRS="mlterm xwindow tool/mlclient man etc scrollbar/sample contrib/scrollbar/extra ${MAKE_DIRS}"
AC_SUBST(MAKE_DIRS)

AC_OUTPUT(Makefile ../mlterm/Makefile ../xwindow/Makefile ../tool/mlconfig/Makefile ../tool/mlclient/Makefile ../man/Makefile ../etc/Makefile ../scrollbar/sample/Makefile ../contrib/scrollbar/extra/Makefile)
