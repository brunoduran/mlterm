AC_INIT()

AC_CONFIG_HEADER(common/c_config.h)
AC_CONFIG_AUX_DIR(script)

if test -d ${top_srcdir-$srcdir}/mkf -a -d ${top_srcdir-$srcdir}/kiklib ; then
	mkdir -p mkf kiklib
	ln -sf ../kiklib ${top_srcdir-$srcdir}/mkf
	ln -sf ../kiklib mkf
	SUBDIRS="kiklib mkf"
	MKF_LIBS='${top_builddir}/mkf/lib/libmkf.la'
	MKF_CFLAGS='-I${top_builddir}/mkf/include'
	KIK_LIBS='${top_builddir}/kiklib/src/libkik.la'
	KIK_CFLAGS='-I${top_builddir}/kiklib/include'
else
	MKF_LIBS="-lmkf"
	KIK_LIBS="-lkik"
fi
AC_SUBST(MKF_LIBS)
AC_SUBST(MKF_CFLAGS)
AC_SUBST(KIK_LIBS)
AC_SUBST(KIK_CFLAGS)

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AC_PROG_CC
AC_PROG_INSTALL

AC_C_INLINE

AC_LIBTOOL_WIN32_DLL
AC_LIBTOOL_DLOPEN
AC_ARG_WITH(libtool,
	[  --with-libtool          libtool path[default=without]],
	libtool=$with_libtool)
if test "${libtool}" != "" ; then
	LIBTOOL=${libtool}
else
	AM_PROG_LIBTOOL
	LIBTOOL='${top_builddir}/libtool'
fi
AC_SUBST(LIBTOOL)

AC_CHECK_LIB(socket,connect,SOCK_LIBS=-lsocket)
AC_SUBST(SOCK_LIBS)

AC_PATH_XTRA

# Li18nux Xlib-I18N defines X_HAVE_UTF8_STRING but has no Xutf8LookupString.
ldflags="$LDFLAGS" cflags="$CFLAGS"
LDFLAGS="$LDFLAGS $X_PRE_LIBS $X_LIBS $X_EXTRA_LIBS" CFLAGS="$X_CFLAGS $CFLAGS"
AC_CHECK_LIB(X11,Xutf8LookupString,XUTF8_CFLAGS="-DHAVE_XUTF8_LOOKUP_STRING")
LDFLAGS="$ldflags" CFLAGS="$cflags"
AC_SUBST(XUTF8_CFLAGS)

AC_ARG_ENABLE(debug,
	[  --enable-debug          debug[default=disabled]],
	debug=$enable_debug)
if test "$debug" = "yes" ; then
	DEB_CFLAGS="-DDEBUG -DKIK_DEBUG"
fi
AC_SUBST(DEB_CFLAGS)
AC_SUBST(OPT_CFLAGS)

AC_ARG_ENABLE(fribidi,
	[  --enable-fribidi        bidi[default=disabled]],
	fribidi=$enable_fribidi, fribidi="no")
if test "$fribidi" = "yes" ; then
	AC_CHECK_PROG(fribidi_config,fribidi-config,yes,fribidi="no")
	if test "$fribidi_config" = "yes" ; then
		FRIBIDI_CFLAGS="`fribidi-config --cflags` -DUSE_FRIBIDI"
		FRIBIDI_LIBS="`fribidi-config --libs`"
	fi
fi
AC_SUBST(FRIBIDI_CFLAGS)
AC_SUBST(FRIBIDI_LIBS)

# default value
IMAGELIB_NAME=none
AC_ARG_WITH(imagelib,
	[  --with-imagelib[=ARG]   image library (imlib|gdk-pixbuf)],
	imagelib=$with_imagelib)
case ${imagelib} in
imlib)
	AC_CHECK_PROG(imlib_config,imlib-config,yes,no)
	if test "x$imlib_config" = "xno" ; then
		echo ""
		echo "** ERROR **"
		echo "Could not find imlib-config."
		echo ""
		exit 1
	fi
	IMAGELIB_CFLAGS="`imlib-config --cflags` -DUSE_IMLIB"
	IMAGELIB_LIBS="`imlib-config --libs`"
	IMAGELIB_NAME=imlib
	;;
gdk-pixbuf)
	PKG_CHECK_MODULES(IMAGELIB, gdk-pixbuf-2.0, [
		IMAGELIB_NAME=gdk
		IMAGELIB_CFLAGS="${IMAGELIB_CFLAGS} -DUSE_GDK_PIXBUF"
	])
	if test "x$IMAGELIB_NAME" = "xnone" ; then
		AC_CHECK_PROG(pkg_config,gdk-pixbuf-config,yes)
		if test "$pkg_config" = "yes" ; then
			IMAGELIB_CFLAGS="`gdk-pixbuf-config --cflags` -DUSE_GDK_PIXBUF"
			IMAGELIB_LIBS="`gdk-pixbuf-config --libs`"
			IMAGELIB_NAME=gdk
		fi
	fi
	;;
gdk-pixbuf2)
	PKG_CHECK_MODULES(IMAGELIB, gdk-pixbuf-2.0, [
		IMAGELIB_NAME=gdk
		IMAGELIB_CFLAGS="${IMAGELIB_CFLAGS} -DUSE_GDK_PIXBUF"
	])
	;;
gdk-pixbuf1)
	AC_CHECK_PROG(pkg_config,gdk-pixbuf-config,yes)
	if test "$pkg_config" = "yes" ; then
		IMAGELIB_CFLAGS="`gdk-pixbuf-config --cflags` -DUSE_GDK_PIXBUF"
		IMAGELIB_LIBS="`gdk-pixbuf-config --libs`"
		IMAGELIB_NAME=gdk
	fi
	;;
*)
	if test "${imagelib}" != "" ; then
		echo ""
		echo "** ERROR **"
		echo "${imagelib} library is NOT supported."
		echo ""
		exit 1
	fi
	;;
esac
AC_SUBST(IMAGELIB_CFLAGS)
AC_SUBST(IMAGELIB_LIBS)
AC_SUBST(IMAGELIB_NAME)

# --enable-anti-alias is backward compatibility for --with-type-engines
AC_ARG_ENABLE(anti_alias,
	[  --enable-anti-alias     (obsoleted by --with-type-engines)anti alias[default=disabled]],
	anti_alias=$enable_anti_alias)
	
AC_ARG_WITH(type_engines,
	[  --with-type-engines     type engines(xcore, xft)[default=xcore]],
	type_engines=$with_type_engines)

if test "$anti_alias" = "yes" ; then
	type_engines="xcore xft"
fi

if test "$type_engines" = ""; then
	type_engines="xcore"
fi

type_engines=`echo ${type_engines} | sed 's/,/ /g'`
for type_engine in ${type_engines} ; do
case ${type_engine} in
xft)
	PKG_CHECK_MODULES(XFT, xft, have_xft=yes, have_xft=no)
	if test "$have_xft" = "yes" ; then
		AA_CFLAGS="-DUSE_TYPE_XFT  ${XFT_CFLAGS} ${AA_CFLAGS}"
		AA_LIBS="${XFT_LIBS} ${AA_LIBS}"
	else
		AA_CFLAGS="-DUSE_TYPE_XFT ${AA_CFLAGS}"
		AA_LIBS="-lXft ${AA_LIBS}"
	fi
	type_engines_result="$type_engines_result xft"
	;;
xcore)
	AA_CFLAGS="-DUSE_TYPE_XCORE ${AA_CFLAGS}"
	type_engines_result="$type_engines_result xcore"
	;;
*)
	echo "${type_engine} is unknown type engine."
	;;
esac
done

if test "${AA_CFLAGS}" = "" ; then
	echo "no type engine specified."
	exit 1
fi

AC_SUBST(AA_CFLAGS)
AC_SUBST(AA_LIBS)
AC_SUBST(PROG)

AC_ARG_ENABLE(ind,
	[  --enable-ind            libind[default=disabled]],
	ind=$enable_ind, ind="no")
if test "$ind" = "yes" ; then
	AC_CHECK_LIB(ind,indian_init,
		[
		IND_CFLAGS="-DUSE_IND"
		IND_LIBS="-lind"
		], ind="no")
fi
AC_SUBST(IND_LIBS)
AC_SUBST(IND_CFLAGS)

AC_ARG_ENABLE(utmp,
	[  --enable-utmp           utmp (Experimental) [default=disabled]],
	utmp=$enable_utmp, utmp="no")
if test "$utmp" = "yes" ; then
	UTMP_CFLAGS="-DUSE_UTMP"
	has_utmp=`grep utmp /etc/group 2>/dev/null`
	if test "$has_utmp" ; then
		INSTALL_OPT="-m 2755 -g utmp"
	else
		INSTALL_OPT="-m 4755 -o root"
	fi
else
	INSTALL_OPT="-m 755"
fi
AC_SUBST(UTMP_CFLAGS)
AC_SUBST(INSTALL_OPT)

PKG_CHECK_MODULES(GTK, gtk+-2.0, have_gtk2=yes, have_gtk2=no)
if test "$have_gtk2" = "yes" ; then
	gtk_config=yes
	GTK_CONFIG="pkg-config gtk+-2.0"
else
	AC_CHECK_PROG(gtk_config,gtk-config,yes)
	if test "$gtk_config" = "yes" ; then
		AC_PATH_PROG(GTK_CONFIG,gtk-config,/usr/local/bin/gtk-config,$PATH)
	else
		# for Daichi Goto-san:)
		AC_CHECK_PROG(gtk_config,gtk12-config,yes)
		if test "$gtk_config" = "yes" ; then
			AC_PATH_PROG(GTK_CONFIG,gtk12-config,/usr/X11R6/bin/gtk12-config,$PATH)
		fi
	fi
	GTK_CFLAGS='gtk-config --cflags'
	GTK_LIBS='gtk-config --libs'

	AC_SUBST(GTK_CFLAGS)
	AC_SUBST(GTK_LIBS)
fi
AC_SUBST(GTK_CONFIG)

tools="mlclient,mlconfig,mlterm-menu,mlcc"
AC_ARG_WITH(tools,
	[  --with-tools[=ARG]      external tools [default=mlclient,mlconfig,mlterm-menu,mlcc]],
	[
	# If given --without-tools or --with-tools with no args.
	if test "${with_tools}" = "no" ; then
		tools=""
	elif test "${with_tools}" != "yes" ; then
		tools=${with_tools}
	fi
	])
tools=`echo ${tools} | sed 's/,/ /g'`
for tool in ${tools} ; do
case ${tool} in
mlconfig)
	if test "${gtk_config}" = "yes" ; then
		MAKE_DIRS="tool/mlconfig ${MAKE_DIRS}"
		OUTPUT_FILES="tool/mlconfig/Makefile tool/mlconfig/po/Makefile.in ${OUTPUT_FILES}"
		tools_result="$tools_result $tool"
	else
		echo ""
		echo "** WARNING  **"
		echo " couldn't compile mlconfig since headers/libraries of GTK are missing."
		echo ""
	fi
	;;
mlterm-menu)
	if test "${gtk_config}" = "yes" ; then
		MAKE_DIRS="contrib/tool/mlterm-menu ${MAKE_DIRS}"
		OUTPUT_FILES="contrib/tool/mlterm-menu/Makefile ${OUTPUT_FILES}"
		tools_result="$tools_result $tool"
	else
		echo ""
		echo "** WARNING  **"
		echo " couldn't compile mlterm-menu since headers/libraries of GTK are missing."
		echo ""
	fi
	;;
mlclient)
	MAKE_DIRS="tool/mlclient ${MAKE_DIRS}"
	OUTPUT_FILES="tool/mlclient/Makefile ${OUTPUT_FILES}"
	tools_result="$tools_result $tool"
	;;
w3mmlconfig)
	AC_PATH_PROG(PERL,perl,$PATH)
	if test "x$PERL" = x ; then
		echo ""
		echo "** WARNING  **"
		echo " couldn't find perl in PATH environment variable."
		echo ""
		PERL="/usr/local/bin/perl" # should be "/usr/bin/env perl"?
	fi
	MAKE_DIRS="tool/w3mmlconfig ${MAKE_DIRS}"
	OUTPUT_FILES="tool/w3mmlconfig/Makefile tool/w3mmlconfig/mlconfig.cgi tool/w3mmlconfig/w3mmlconfig ${OUTPUT_FILES}"
	tools_result="$tools_result $tool"
	;;
mlcc)
	MAKE_DIRS="contrib/tool/mlcc ${MAKE_DIRS}"
	OUTPUT_FILES="contrib/tool/mlcc/Makefile ${OUTPUT_FILES}"
	tools_result="$tools_result $tool"
	;;
mlmenu)
	MAKE_DIRS="tool/mlmenu ${MAKE_DIRS}"
	OUTPUT_FILES="tool/mlmenu/Makefile ${OUTPUT_FILES}"
	tools_result="$tools_result $tool"
	;;
mlterm-zoom)
	MAKE_DIRS="contrib/tool/mlterm-zoom ${MAKE_DIRS}"
	OUTPUT_FILES="contrib/tool/mlterm-zoom/Makefile ${OUTPUT_FILES}"
	tools_result="$tools_result $tool"
	;;
*)
	echo "${tool} is unknown tool."
	;;
esac
done

#
# --- libltdl ---
#
AC_ARG_WITH(libltdl,
	[  --with-libltdl[=PREFIX] load modules with libltdl[default=without]],,
	[with_libltdl=no])
# We don't check here wheter libltdl is installed. (See kiklib/src/configure.in)

#
# --- Xdnd ---
#
AC_ARG_ENABLE(dnd,
	[  --disable-dnd           dnd [default=enabled]],
	dnd=$enable_dnd, dnd="yes")
if test "$dnd" = "no" ; then
	DND_CFLAGS="-DDISABLE_XDND"
else
	DND_LIBS="x_dnd.o"
fi
AC_SUBST(DND_CFLAGS)
AC_SUBST(DND_LIBS)


IM_CFLAGS=""
DL_LIBS_IM=""

#
# --- kbd ---
#
AC_ARG_ENABLE(kbd,
	[  --disable-kbd           input method for Arabic and Indic [default=enabled]],
	kbd=$enable_kbd)
if test "$kbd" != "no" ; then
	IM_CFLAGS="$IM_CFLAGS -DUSE_KBD"
	MAKE_DIRS="inputmethod/kbd ${MAKE_DIRS}"
	OUTPUT_FILES="inputmethod/kbd/Makefile ${OUTPUT_FILES}"
	if test "x$with_libltdl" != "xno" ; then
		DL_LIBS_IM="-dlopen \$(top_builddir)/inputmethod/kbd/libim-kbd.la ${DL_LIBS_IM}"
	fi
	input_methods_result="$input_methods_result kbd"
fi

#
# --- uim ---
#
AC_ARG_ENABLE(uim,
	[  --enable-uim            uim (Experimental) [default=disabled]],
	uim=$enable_uim)
if test "x$uim" = xyes ; then
	PKG_CHECK_MODULES(UIM, uim, have_uim=yes, have_uim=no)
	if test "x$have_uim" = xyes ; then
		IM_CFLAGS="$IM_CFLAGS -DUSE_UIM"
		MAKE_DIRS="inputmethod/uim ${MAKE_DIRS}"
		OUTPUT_FILES="inputmethod/uim/Makefile ${OUTPUT_FILES}"
		ml_saved_libs="$LIBS"
		LIBS="$UIM_LIBS"
		AC_CHECK_LIB(uim, uim_get_default_im_name,
			     [
			     UIM_CFLAGS="$UIM_CFLAGS -DUIM_0_4_4_OR_LATER"
			     ])
		AC_CHECK_LIB(uim, uim_prop_update_custom,
			     [
			     UIM_CFLAGS="$UIM_CFLAGS -DUIM_0_4_6_OR_LATER"
			     ])
		LIBS="$ml_saved_libs"
		if test "x$with_libltdl" != xno ; then
			DL_LIBS_IM="-dlopen \$(top_builddir)/inputmethod/uim/libim-uim.la ${DL_LIBS_IM}"
		fi
		input_methods_result="$input_methods_result uim"
	fi
fi
AC_SUBST(UIM_CFLAGS)
AC_SUBST(UIM_LIBS)

#
# --- iiimf ---
#
AC_ARG_ENABLE(iiimf,
	[  --enable-iiimf          IIIMF (Experimental) [default=disabled]],
	iiimf=$enable_iiimf)
if test "x$iiimf" = xyes ; then
	if test -d /usr/lib/im/lib ; then
		LIBS="$LIBS -L/usr/lib/im/lib"
		AC_CHECK_LIB(iiimcf, iiimcf_create_context,
			[
			found_iiimcf=yes
			IIIMF_CFLAGS="-I/usr/lib/im/include"
			IIIMF_LIBS="-L/usr/lib/im/lib -liiimcf"
			])
	else
		AC_CHECK_LIB(iiimcf, iiimcf_create_context,
			[
			found_iiimcf=yes
			IIIMF_CFLAGS=""
			IIIMF_LIBS="-liiimcf"
			])
	fi

	if test "x$found_iiimcf" = xyes ; then
		IM_CFLAGS="$IM_CFLAGS -DUSE_IIIMF"
		MAKE_DIRS="inputmethod/iiimf ${MAKE_DIRS}"
		OUTPUT_FILES="inputmethod/iiimf/Makefile ${OUTPUT_FILES}"
		if test "x$with_libltdl" != xno ; then
			DL_LIBS_IM="-dlopen \$(top_builddir)/inputmethod/iiimf/libim-iiimf.la ${DL_LIBS_IM}"
		fi
		input_methods_result="$input_methods_result IIIMF"
		AC_CHECK_LIB(iiimcf, iiimcf_create_hotkey_notify_event,
			[
			IIIMF_CFLAGS="$IIIMF_CFLAGS -DHAVE_HOTKEY_NOTFY_EVENT"
			])
		AC_CHECK_LIB(iiimcf, iiimcf_create_aux_getvalues_event,
			[
			IIIMF_CFLAGS="$IIIMF_CFLAGS -DHAVE_AUX_GETVALUES_EVENT"
			])
	fi
fi
AC_SUBST(IIIMF_CFLAGS)
AC_SUBST(IIIMF_LIBS)

#
# --- m17n library ---
#
AC_ARG_ENABLE(m17nlib,
	[  --enable-m17nlib        m17n library (Experimental) [default=disabled]],
	m17nlib=$enable_m17nlib)
if test "x$m17nlib" = xyes ; then
	AC_CHECK_PROG(m17nlib, m17n-config, yes, no)
fi
if test "x$m17nlib" = xyes ; then
	m17n_saved_libs="$LIBS"
	M17NLIB_LIBS=`m17n-config --libs`
	LIBS="$LIBS $M17NLIB_LIBS"
	AC_CHECK_LIB(m17n, minput_open_im,
		[
		IM_CFLAGS="$IM_CFLAGS -DUSE_M17NLIB"
		M17NLIB_CFLAGS="`m17n-config --cflags`"
		MAKE_DIRS="inputmethod/m17nlib ${MAKE_DIRS}"
		OUTPUT_FILES="inputmethod/m17nlib/Makefile ${OUTPUT_FILES}"
		if test "x$with_libltdl" != xno ; then
			DL_LIBS_IM="-dlopen \$(top_builddir)/inputmethod/m17nlib/libim-m17nlib.la ${DL_LIBS_IM}"
		fi
		input_methods_result="$input_methods_result m17nlib"
		])
	LIBS="$m17n_saved_libs"
fi
AC_SUBST(M17NLIB_CFLAGS)
AC_SUBST(M17NLIB_LIBS)

#
# --- SCIM ---
#
AC_ARG_ENABLE(scim,
	[  --enable-scim           SCIM (Experimental) [default=disabled]],
	scim=$enable_scim)
if test "x$scim" = "xyes" ; then
	AC_PROG_CXX
	PKG_CHECK_MODULES(SCIM, scim >= 1.1.0, have_scim=yes , have_scim=no)
	if test "x$have_scim" = xyes ; then
		dnl welcome to #ifdef hell.
		SCIM_CFLAGS="$SCIM_CFLAGS -DSCIM_1_2_0"
	else
		PKG_CHECK_MODULES(SCIM, scim >= 1.0.0, have_scim=yes, have_scim=no)
	fi
	if test "x$have_scim" = xyes ; then
		IM_CFLAGS="$IM_CFLAGS -DUSE_SCIM"
		MAKE_DIRS="inputmethod/scim ${MAKE_DIRS}"
		OUTPUT_FILES="inputmethod/scim/Makefile ${OUTPUT_FILES}"
		if test "x$with_libltdl" != xno ; then
			DL_LIBS_IM="-dlopen \$(top_builddir)/inputmethod/scim/libim-scim.la ${DL_LIBS_IM}"
		fi
		input_methods_result="$input_methods_result SCIM"
	fi
fi
AC_SUBST(SCIM_CFLAGS)
AC_SUBST(SCIM_LIBS)


if test "x$IM_CFLAGS" != x ; then
	IM_CFLAGS="$IM_CFLAGS -DUSE_IM_PLUGIN"
fi
AC_SUBST(IM_CFLAGS)
AC_SUBST(DL_LIBS_IM)


#
# --- scrollbar plugins ---
#

DL_LIBS_SB=""

scrollbars="sample,extra"  # default

AC_ARG_WITH(scrollbars,
	[  --with-scrollbars[=ARG] scrollbar plugins (sample, extra, pixmap_engine)[default=sample,extra]],
	[
	# If given --without-scrollbars or --with-scrollbars with no args.
	if test "${with_scrollbars}" = "no" ; then
		scrollbars=""
	elif test "${with_scrollbars}" != "yes" ; then
		scrollbars=${with_scrollbars}
	fi
	])
scrollbars=`echo ${scrollbars} | sed 's/,/ /g'`
for scrollbar in ${scrollbars} ; do
case ${scrollbar} in
sample)
	MAKE_DIRS="scrollbar/sample ${MAKE_DIRS}"
	OUTPUT_FILES="scrollbar/sample/Makefile ${OUTPUT_FILES}"
	if test "x$with_libltdl" != "xno" ; then
		for name in sample sample2 ; do
			DL_LIBS_SB="-dlopen \$(top_builddir)/scrollbar/sample/lib$name.la ${DL_LIBS_SB}"
		done
	fi
	scrollbars_result="$scrollbars_result $scrollbar"
	;;
extra)
	MAKE_DIRS="contrib/scrollbar/extra ${MAKE_DIRS}"
	OUTPUT_FILES="contrib/scrollbar/extra/Makefile ${OUTPUT_FILES}"
	if test "x$with_libltdl" != "xno" ; then
		for name in athena next motif mozmodern ; do
			DL_LIBS_SB="-dlopen \$(top_builddir)/contrib/scrollbar/extra/lib$name.la ${DL_LIBS_SB}"
		done
	fi
	scrollbars_result="$scrollbars_result $scrollbar"
	;;
pixmap_engine)
	if test "x$allow_undefined_flag" = "xunsupported" ; then
		echo ""
		echo "** ERROR **"
		echo "pixmap_engine has undefined symbols. $host_os does NOT allow it. Please retry"
		echo "to run configure scripts without pixmap_engine"
		echo "(e.g.  --with-scrllbars=sample,extra)"
		echo ""
		exit 1
	fi
	if test "$IMAGELIB_NAME" = "gdk" -o "$IMAGELIB_NAME" = "imlib" ; then
		MAKE_DIRS="contrib/scrollbar/pixmap_engine ${MAKE_DIRS}"
		OUTPUT_FILES="contrib/scrollbar/pixmap_engine/Makefile contrib/scrollbar/pixmap_engine/sample3/Makefile ${OUTPUT_FILES}"
		if test "x$with_libltdl" != "xno" ; then
			DL_LIBS_SB="-export-dynamic -dlopen \$(top_builddir)/contrib/scrollbar/pixmap_engine/libpixmap_engine.la ${DL_LIBS_SB}"
		else
			DL_LIBS_SB="-export-dynamic"
		fi
		scrollbars_result="$scrollbars_result $scrollbar"
	else
		echo ""
		echo "** ERROR **"
		echo "To use pixmap_engine for scrollbar, you need any image library such as"
		echo "gdk-pixbuf and imlib. (e.g. --with-imagelib=imlib)"
		echo ""
		exit 1
	fi
	;;
*)
	echo "${scrollbar} is unknown scrollbar."
	;;
esac
done # for scrollbar in ${scrollbars}

AC_SUBST(DL_LIBS_SB)

AC_ARG_ENABLE(optimize_redrawing,
	[  --enable-optimize-redrawing optimize redrawing a line[default=disabled]],
	optimize_redrawing=$enable_optimize_redrawing,
	optimize_redrawing="no")
if test "$optimize_redrawing" = "yes" ; then
	CFLAGS="$CFLAGS -DOPTIMIZE_REDRAWING"
fi

case "${host_os}" in
mingw32*)
	MAKE_DIRS="mlterm ${MAKE_DIRS}"
#	MAKE_DIRS="${MAKE_DIRS} win32"
	OUTPUT_FILES="Makefile mlterm/Makefile etc/Makefile"
#	OUTPUT_FILES="${OUTPUT_FILES} win32/Makefile"
	ML_PTY_OBJ="ml_pty_win32.o"
	PLATFORM_LIB_LDFLAGS="-no-undefined"
	;;
*)
	MAKE_DIRS="mlterm ${MAKE_DIRS} xwindow man etc"
	OUTPUT_FILES="Makefile mlterm/Makefile xwindow/Makefile man/Makefile etc/Makefile ${OUTPUT_FILES}"
	ML_PTY_OBJ="ml_pty.o"
	PLATFORM_LIB_LDFLAGS=""
	;;
esac
AC_SUBST(ML_PTY_OBJ)
AC_SUBST(MAKE_DIRS)
AC_SUBST(OUTPUT_FILES)
AC_SUBST(PLATFORM_LIB_LDFLAGS)

# gettext
AM_INIT_AUTOMAKE(mlterm,2.9.1)
AM_GNU_GETTEXT([external])

# XXX(maybe not portable)
if test "${USE_NLS}" = "yes" ; then
	AC_CHECK_LIB(intl,gettext,
		[
		INTL_LIBS="-lintl"
		])
fi
AC_SUBST(INTL_LIBS)


#
# --- module related stuffs ---
#

# undefined symbol
AC_MSG_CHECKING([for undefined symbol])
if test "x$allow_undefined_flag" = "xunsupported" ; then
	AC_MSG_RESULT([not supported])
	NO_UNDEFINED_FLAG="-no-undefined"
	KIK_LIBS_FOR_MODULE='-L${top_builddir}/kiklib/src -lkik'
	MKF_LIBS_FOR_MODULE='-L${top_builddir}/mkf/lib -lmkf'
else
	AC_MSG_RESULT(supported)
	NO_UNDEFINED_FLAG=""
	KIK_LIBS_FOR_MODULE=""
	MKF_LIBS_FOR_MODULE=""
fi
AC_SUBST(NO_UNDEFINED_FLAG)
AC_SUBST(KIK_LIBS_FOR_MODULE)
AC_SUBST(MKF_LIBS_FOR_MODULE)

if test "x$with_libltdl" != "xno" ; then
	DL_SELF="-dlopen self"
fi
AC_SUBST(DL_SELF)


# CVS revision
CVS_REVISION=`tail -n 1 ${top_srcdir-$srcdir}/ChangeLog | sed -n 's/^.Id: .* \(.*\) .* .* .* Exp .$/\1/p'`
AC_SUBST(CVS_REVISION)
OUTPUT_FILES="xwindow/version.h ${OUTPUT_FILES}"

if test "${SUBDIRS}" != "" ; then
	AC_CONFIG_SUBDIRS(${SUBDIRS})
fi

if test "${MAKE_DIRS}" != "" ; then
	mkdir -p ${MAKE_DIRS}
fi

AC_OUTPUT(${OUTPUT_FILES})

# configuration result

echo
echo
echo "Mlterm was configured as follows"
echo
echo "Installation path prefix          : $prefix"
dnl echo "Character mapping table           : "
dnl echo "Character property table          : "
echo "Build shared libraries            : $enable_shared"
echo "Build static libraries            : $enable_static"
echo "Optimization for redrawing a line : $optimize_redrawing"
echo "BiDi rendering (Fribidi)          : $fribidi"
echo "ISCII rendering (libind)          : $ind"
echo "External tools                    :$tools_result"
echo "Image library                     : $with_imagelib"
echo "utmp support                      : $utmp"
echo "Type engines                      :$type_engines_result"
echo "DnD                               : $dnd"
echo "Input Methods                     : XIM$input_methods_result"
echo "Scrollbars                        : simple${scrollbars_result}"
echo

