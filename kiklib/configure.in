AC_INIT()

AC_CONFIG_HEADER(src/kik_config.h)
AC_CONFIG_AUX_DIR(script)

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AC_PROG_CC
dnl  if --with-libtool is specified, cpp isn't detected.
AC_PROG_CPP
AC_PROG_INSTALL

dnl
dnl --- libtool ---
dnl
AC_LIBTOOL_WIN32_DLL
AC_LIBTOOL_DLOPEN
AC_ARG_WITH(libtool,
	[  --with-libtool          libtool path[without]],
	libtool=$with_libtool)
if test "${libtool}" != "" ; then
	LIBTOOL=${libtool}
else
	AM_PROG_LIBTOOL
	LIBTOOL='${top_builddir}/libtool'
fi
AC_SUBST(LIBTOOL)

dnl
dnl --- Checks for header files ---
dnl
dnl NOTE: if --with-libtool is specified, stdc header isn't detected.
AC_HEADER_STDC
AC_CHECK_HEADERS(langinfo.h dlfcn.h dl.h stropts.h sys/stropts.h)

dnl
dnl --- Checks for library functions ---
dnl
AC_CHECK_FUNCS(strsep fgetln basename isastream seteuid setegid geteuid getegid snprintf usleep unsetenv flock)
AC_FUNC_ALLOCA

AC_C_INLINE
AC_C_CONST
AC_C_BIGENDIAN

AC_CHECK_TYPE(u_char,unsigned char)
AC_CHECK_TYPE(u_short,unsigned short)
AC_CHECK_TYPE(u_int,unsigned int)
AC_CHECK_TYPE(u_long,unsigned long)
AC_CHECK_TYPE(u_int8_t,unsigned char)
AC_CHECK_TYPE(u_int16_t,unsigned short)
AC_CHECK_TYPE(u_int32_t,unsigned int)
AC_CHECK_TYPE(u_int64_t,unsigned long)
AC_CHECK_TYPE(int8_t,char)
AC_CHECK_TYPE(int16_t,short)
AC_CHECK_TYPE(int32_t,int)
AC_CHECK_TYPE(int64_t,long)

AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_UID_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl
dnl --- Platform dependent stuff ---
dnl
kik_cv_cygwin=no
kik_cv_mingw32=no
case "${host_os}" in
cygwin*)
	kik_cv_cygwin=yes
	;;
mingw32*)
	kik_cv_mingw32=yes
	AC_DEFINE(USE_WIN32API)
	;;
*)
	;;
esac

dnl
dnl --- socklen_t ---
dnl
AC_CACHE_CHECK(for socklen_t, kik_cv_socklen_ident,
[AC_TRY_COMPILE(
[
#include  <sys/socket.h>
],
[
socklen_t  len ;
], kik_cv_socklen_ident=yes, kik_cv_socklen_ident=no)])
if test "${kik_cv_socklen_ident}" = "no" ; then
	AC_DEFINE(socklen_t, unsigned int)
fi


dnl
dnl --- __FUNCTION__ ---
dnl
AC_CACHE_CHECK(for __FUNCTION__, kik_cv_func_ident,[
AC_TRY_COMPILE(,
[
char * p = "[" __FUNCTION__ "]" ;
] , kik_cv_func_ident=yes,kik_cv_func_ident=no)])
if test "${kik_cv_func_ident}" = "yes" ; then
	AC_DEFINE(HAVE_FUNCTION)
fi


dnl
dnl --- Check for libxpg4 (for FreeBSD) ---
dnl
AC_CHECK_LIB(xpg4, setlocale, XPG4_LIBS=-lxpg4)
AC_SUBST(XPG4_LIBS)


dnl
dnl --- Checks for dynamic linking loader ---
dnl
DL_LOADER=none
DL_LIBS=
DL_CFLAGS=
dnl lt_dlopenext in libltdl
AC_ARG_WITH(libltdl,
	[  --with-libltdl[=PREFIX] load modules with libltdl[without]],,
	[with_libltdl=no])
if test "x$with_libltdl" != "xno" ; then
	if test "x$with_libltdl" != "xyes"; then
		DL_CFLAGS="-I$with_libltdl/include"
		kik_libltdl_libdir="-L$with_libltdl/lib"
	fi
	kik_ldflags_save="$LDFLAGS"
	LDFLAGS="$LDFLAGS $kik_libltdl_libdir"
	AC_CHECK_LIB(ltdl, lt_dlopenext,
			[
			DL_LOADER=ltdl
			DL_LIBS="$kik_libltdl_libdir -lltdl"
			],[
			echo ""
			echo "Could not find libltdl"
			echo ""
			exit 1])
	LDFLAGS="$kik_ldflags_save"
fi
dnl lt_cv_dlopen is set by AC_LIBTOOL_DLOPEN
if test "$DL_LOADER" = none ; then
	case "${lt_cv_dlopen}" in
	dnl LoadLibrary  (Windows)
	LoadLibrary)
		DL_LOADER=win32
		;;
	dnl shl_load (HP-UX)
	shl_load)
		DL_LOADER=dld
		;;
	dnl dlopen (UNIX98)
	dlopen)
		DL_LOADER=dl
		;;
	dnl What is dld_link? Does anybody know?
	dld_link)
		DL_LOADER=none
		;;
	*)
		DL_LOADER=none
		;;
	esac
	DL_LIBS="${DL_LIBS} ${lt_cv_dlopen_libs}"
	dnl NSLinkModule (darwin)
	AC_CHECK_FUNC(NSLinkModule,
			[
			DL_LOADER=dyld
			DL_LIBS=
			], [])
fi
AC_SUBST(DL_LOADER)
AC_SUBST(DL_LIBS)
AC_SUBST(DL_CFLAGS)


dnl
dnl --- checks for utmp ---
dnl
UTMP_NAME=
UTMP_LIBS=
dnl  Windows
if test "$kik_cv_mingw32" = yes -o "$kik_cv_cygwin" = yes ; then
	UTMP_NAME=none
	UTMP_LIBS=
fi
dnl  logout() in libutil
if test -z "$UTMP_NAME" ; then
	AC_CHECK_LIB(util, logout,
		[
		UTMP_NAME=login
		UTMP_LIBS=-lutil
		], [])
fi
dnl  setutent()  (SysV)
if test -z "$UTMP_NAME" ; then
	AC_CHECK_FUNC(setutent,
		[
		UTMP_NAME=sysv
		UTMP_LIBS=
		], [])
fi
dnl  addToUtmp() in libutempter
if test -z "$UTMP_NAME" ; then
	AC_CHECK_LIB(utempter, addToUtmp,
		[
		UTMP_NAME=utmper
		UTMP_LIBS="-lutempter -lutil"
		]. [])
fi
dnl  other (BSD)
if test -z "$UTMP_NAME" ; then
	UTMP_NAME=bsd
	UTMP_LIBS=
fi
AC_SUBST(UTMP_NAME)
AC_SUBST(UTMP_LIBS)


dnl
dnl --- pty checks ---
dnl
AC_MSG_CHECKING(for pty/tty type)
AC_CACHE_VAL(kik_cv_pty,
	[
	if test "$kik_cv_mingw32" = yes ; then
		kik_cv_pty=win32
	else
		if test -c /dev/ptmx ; then
			kik_cv_pty=streams
		else
			kik_cv_pty=bsd
		fi
	fi])
AC_MSG_RESULT($kik_cv_pty)
PTY_NAME="${kik_cv_pty}"
AC_SUBST(PTY_NAME)


dnl
dnl --- debug ---
dnl
AC_ARG_ENABLE(debug,
	[  --enable-debug          debug[disabled]],
	debug=$enable_debug)
if test "$debug" = yes ; then
	DEB_CFLAGS="-DDEBUG -DKIK_DEBUG"
fi
AC_SUBST(DEB_CFLAGS)

AC_OUTPUT(Makefile src/Makefile)
