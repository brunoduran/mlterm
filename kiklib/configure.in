AC_INIT()

AC_CONFIG_HEADER(src/kik_config.h)
AC_CONFIG_AUX_DIR(script)

AC_PROG_CC
# if --with-libtool is specified, cpp isn't detected.
AC_PROG_CPP
AC_PROG_INSTALL

AC_ARG_WITH(libtool,
	[ --with-libtool		libtool path[without]],
	libtool=$with_libtool)
if test "${libtool}" != "" ; then
	LIBTOOL=${libtool}
else
	AM_PROG_LIBTOOL
	LIBTOOL='${top_builddir}/libtool'
fi
AC_SUBST(LIBTOOL)

# if --with-libtool is specified, stdc header isn't detected.
AC_HEADER_STDC

AC_CHECK_HEADERS(langinfo.h dlfcn.h dl.h stropts.h sys/stropts.h)

AC_CHECK_FUNCS(strsep fgetln basename isastream setutent seteuid setegid geteuid getegid snprintf usleep unsetenv)

AC_FUNC_ALLOCA

AC_C_INLINE

AC_C_CONST

AC_CHECK_TYPE(u_char,unsigned char)
AC_CHECK_TYPE(u_short,unsigned short)
AC_CHECK_TYPE(u_int,unsigned int)
AC_CHECK_TYPE(u_long,unsigned long)
AC_CHECK_TYPE(u_int8_t,unsigned char)
AC_CHECK_TYPE(u_int16_t,unsigned short)
AC_CHECK_TYPE(u_int32_t,unsigned int)
AC_CHECK_TYPE(u_int64_t,unsigned long)
AC_CHECK_TYPE(int8_t,char)
AC_CHECK_TYPE(int16_t,short)
AC_CHECK_TYPE(int32_t,int)
AC_CHECK_TYPE(int64_t,long)

AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_UID_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

AC_CACHE_CHECK(for socklen_t , kik_cv_socklen_ident,[
AC_TRY_COMPILE(
[
#include  <sys/socket.h>
],
[
socklen_t  len ;
],kik_cv_socklen_ident=yes,kik_cv_socklen_ident=no)])
if test "${kik_cv_socklen_ident}" = "no" ; then
	AC_DEFINE(socklen_t,unsigned int)
fi

AC_CACHE_CHECK(for __FUNCTION__ , kik_cv_func_ident,[
AC_TRY_COMPILE(,
[
char * p = __FUNCTION__ ;
],kik_cv_func_ident=yes,kik_cv_func_ident=no)])
if test "${kik_cv_func_ident}" = "yes" ; then
	AC_DEFINE(HAVE_FUNCTION)
fi

AC_CHECK_LIB(xpg4,setlocale,XPG4_LIBS=-lxpg4)
AC_SUBST(XPG4_LIBS)

AC_CHECK_LIB(dl,dlopen,DL_LIBS=-ldl)
AC_SUBST(DL_LIBS)

AC_CHECK_LIB(util,logout,
[
LOGIN_LIBS="-lutil"
AC_SUBST(LOGIN_LIBS)
kik_cv_utmp=login
])

AC_CYGWIN
if test "$CYGWIN" = "yes" ; then
	kik_cv_utmp=none
elif test -z "$kik_cv_utmp" ; then
	if test "$ac_cv_func_setutent" = yes ; then
		kik_cv_utmp=sysv
	else
		kik_cv_utmp=bsd
	fi
fi

AC_CHECK_LIB(utempter,addToUtmp,
[
UTMPER_LIBS="-lutempter -lutil"
AC_SUBST(UTMPER_LIBS)
kik_cv_utmp=utmper
])

UTMP_NAME=$kik_cv_utmp
AC_SUBST(UTMP_NAME)

AC_MSG_CHECKING(for pty/tty type)
AC_CACHE_VAL(kik_cv_pty,[
if test -c /dev/ptmx ; then
	kik_cv_pty="streams"
else
	kik_cv_pty="bsd"
fi])
AC_MSG_RESULT($kik_cv_pty)

PTY_NAME="${kik_cv_pty}"
AC_SUBST(PTY_NAME)

AC_ARG_ENABLE(debug,
	[ --enable-debug	debug[disabled]],
	debug=$enable_debug)
if test "$debug" = "yes" ; then
	DEB_CFLAGS="-DDEBUG -DKIK_DEBUG"
fi
AC_SUBST(DEB_CFLAGS)

AC_OUTPUT(Makefile src/Makefile)
