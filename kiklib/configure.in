AC_INIT()

AC_CONFIG_HEADER(src/kik_config.h)
AC_CONFIG_AUX_DIR(script)

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AC_PROG_CC
dnl  if --with-libtool is specified, cpp isn't detected.
AC_PROG_CPP
AC_PROG_INSTALL

dnl
dnl --- libtool ---
dnl
AC_LIBTOOL_WIN32_DLL
AC_ARG_WITH(libtool,
	[  --with-libtool          libtool path[without]],
	libtool=$with_libtool)
if test "${libtool}" != "" ; then
	LIBTOOL=${libtool}
else
	AM_PROG_LIBTOOL
	LIBTOOL='${top_builddir}/libtool'
fi
AC_SUBST(LIBTOOL)

dnl
dnl --- header checks ---
dnl
dnl NOTE: if --with-libtool is specified, stdc header isn't detected.
AC_HEADER_STDC
AC_CHECK_HEADERS(langinfo.h dlfcn.h dl.h stropts.h sys/stropts.h)

dnl
dnl --- check for library functions ---
dnl
AC_CHECK_FUNCS(strsep fgetln basename isastream seteuid setegid geteuid getegid snprintf usleep unsetenv flock)
AC_FUNC_ALLOCA

AC_C_INLINE
AC_C_CONST

AC_CHECK_TYPE(u_char,unsigned char)
AC_CHECK_TYPE(u_short,unsigned short)
AC_CHECK_TYPE(u_int,unsigned int)
AC_CHECK_TYPE(u_long,unsigned long)
AC_CHECK_TYPE(u_int8_t,unsigned char)
AC_CHECK_TYPE(u_int16_t,unsigned short)
AC_CHECK_TYPE(u_int32_t,unsigned int)
AC_CHECK_TYPE(u_int64_t,unsigned long)
AC_CHECK_TYPE(int8_t,char)
AC_CHECK_TYPE(int16_t,short)
AC_CHECK_TYPE(int32_t,int)
AC_CHECK_TYPE(int64_t,long)

AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_UID_T
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl
dnl --- platform dependent stuff ---
dnl
kik_cv_cygwin=no
kik_cv_mingw32=no
case "${target_os}" in
cygwin*)
	kik_cv_cygwin=yes
	;;
mingw32*)
	kik_cv_mingw32=yes
	AC_DEFINE(USE_WIN32API)
	;;
darwin*|rhapsody*)
	;;
*)
	;;
esac


dnl
dnl --- socklen_t ---
dnl
AC_CACHE_CHECK(for socklen_t, kik_cv_socklen_ident,
[AC_TRY_COMPILE(
[
#include  <sys/socket.h>
],
[
socklen_t  len ;
], kik_cv_socklen_ident=yes, kik_cv_socklen_ident=no)])
if test "${kik_cv_socklen_ident}" = "no" ; then
	AC_DEFINE(socklen_t, unsigned int)
fi


dnl
dnl --- __FUNCTION__ ---
dnl
AC_CACHE_CHECK(for __FUNCTION__, kik_cv_func_ident,[
AC_TRY_COMPILE(,
[
char * p = "[" __FUNCTION__ "]" ;
] , kik_cv_func_ident=yes,kik_cv_func_ident=no)])
if test "${kik_cv_func_ident}" = "yes" ; then
	AC_DEFINE(HAVE_FUNCTION)
fi


dnl
dnl --- libxpg4 check (for FreeBSD) ---
dnl
AC_CHECK_LIB(xpg4, setlocale, XPG4_LIBS=-lxpg4)
AC_SUBST(XPG4_LIBS)


dnl
dnl --- dynamic linking loader checks ---
dnl
DL_MODULE=none
DL_LIBS=
dnl LoadLibrary()  (Windows)
if test "$kik_cv_mingw32" = yes -o "$kik_cv_cygwin" = yes ; then
	DL_MODULE=win32
	DL_LIBS=
fi
dnl dlopen()
if test "$DL_MODULE" = none ; then
	AC_CHECK_FUNC(dlopen,
			[
			DL_MODULE=dl
			DL_LIBS=
			], [])
fi
dnl dlopen() in libdl
if test "$DL_MODULE" = none ; then
	AC_CHECK_LIB(dl, dlopen,
			[
			DL_MODULE=dl
			DL_LIBS=-ldl
			], [])
fi
dnl shl_load()
if test "$DL_MODULE" = none ; then
	AC_CHECK_FUNC(shl_load,
			[
			DL_MODULE=dld
			DL_LIBS=
			], [])
fi
dnl shl_load() in lildld
if test "$DL_MODULE" = none ; then
	AC_CHECK_LIB(dld, shl_load,
			[
			DL_MODULE=dld
			DL_LIBS=-ldld
			], [])
fi
dnl Darwin NSLinkModule
if test "$DL_MODULE" = none ; then
	AC_CHECK_FUNC(NSLinkModule,
			[
			DL_MODULE=dyld
			DL_LIBS=
			], [])
fi
AC_SUBST(DL_MODULE)
AC_SUBST(DL_LIBS)


dnl
dnl --- utmp checks ---
dnl
UTMP_NAME=
UTMP_LIBS=
dnl  Windows
if test "$kik_cv_mingw32" = yes -o "$kik_cv_cygwin" = yes ; then
	UTMP_NAME=none
	UTMP_LIBS=
fi
dnl  logout() in libutil
if test -z "$UTMP_NAME" ; then
	AC_CHECK_LIB(util, logout,
		[
		UTMP_NAME=login
		UTMP_LIBS=-lutil
		], [])
fi
dnl  setutent()  (SysV)
if test -z "$UTMP_NAME" ; then
	AC_CHECK_FUNC(setutent,
		[
		UTMP_NAME=sysv
		UTMP_LIBS=
		], [])
fi
dnl  addToUtmp() in libutempter
if test -z "$UTMP_NAME" ; then
	AC_CHECK_LIB(utempter, addToUtmp,
		[
		UTMP_NAME=utmper
		UTMP_LIBS="-lutempter -lutil"
		]. [])
fi
dnl  other (BSD)
if test -z "$UTMP_NAME" ; then
	UTMP_NAME=bsd
	UTMP_LIBS=
fi
AC_SUBST(UTMP_NAME)
AC_SUBST(UTMP_LIBS)


dnl
dnl --- pty checks ---
dnl
AC_MSG_CHECKING(for pty/tty type)
AC_CACHE_VAL(kik_cv_pty,
	[
	if test "$kik_cv_mingw32" = yes ; then
		kik_cv_pty=win32
	else
		if test -c /dev/ptmx ; then
			kik_cv_pty=streams
		else
			kik_cv_pty=bsd
		fi
	fi])
AC_MSG_RESULT($kik_cv_pty)
PTY_NAME="${kik_cv_pty}"
AC_SUBST(PTY_NAME)


dnl
dnl --- debug ---
dnl
AC_ARG_ENABLE(debug,
	[  --enable-debug          debug[disabled]],
	debug=$enable_debug)
if test "$debug" = yes ; then
	DEB_CFLAGS="-DDEBUG -DKIK_DEBUG"
fi
AC_SUBST(DEB_CFLAGS)


AC_OUTPUT(Makefile src/Makefile)
